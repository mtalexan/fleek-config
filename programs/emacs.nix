{ pkgs, misc, lib, config, inputs, ... }:
# We are using the emacsWithPackagesFromUsePackage from the nix-community emacs overlay to have
# our `use-package` emacs packages automatically installed as nix packages from the nix-community
# emacs overlay instead.
# Our primary config is a literate emacs org-mode file that is tangled at build-time to generate the
# actual elisp config file.
# There are some settings that can only be set in a user-specific config file, while the normal tools
# will install our main config as a site-lisp/default.el that's within the nix store.
#
# The following config files are used/provided by this module:
#  1. site-config.org - Generated from the custom.emacs.siteConfigOrg variable and set as the site-lisp/default.el.
#     All use-packages from this are installed as nix packages at build-time.
#     This file gets symlinked from it's generated nix store location to ~/.emacs.d/site-config.org for visibility,
#     but is not actually loaded from this location (or the location symlinked to) at run-time.
#  2. site-writeable.org - A writeable tangled org-mode file for testing custom changes before they're included in
#     the site-config.org. This file is loaded from the site-config.org itself, but lives in this config repo.
#     Any use-package declarations in this file are only processed by emacs at run-time, not at nix build-time.
#  3. init.el - Generated from the custom.emacs.configEl variable. This is a user-specific config file that gets
#     written to ~/.emacs.d/init.el. This file is primarily for settings that can only be set in a user-specific
#     config files.
#  4. custom.el - A file generated by emacs itself at run-time into the ~/.emacs.d/ whenever a setting is changed via
#     the GUI menus. The init.el is configured to load this file if it exists.
#  5. writeable.org - A writeable tangled org-mode file for testing custom changes before they're included in the
#     init.el. This file is loaded from the init.el itself, but lives in this config repo.
#
# WARNING: emacs installed via Nix suffers from an issue on SSSD systems where it's unaware of the SSSD users, so libnss lookups
#          will get './~$USER' as the users home folder instead of what's correct.  To solve this specifcially for
#          emacs, we can call 'emacs --user ""' and it works to find the correct home folder.
#          Set in the host-specific config home.shellAliases:
#             "emacs" = ''${config.programs.emacs.package}/bin/emacs --user "" '';
let
  # This will create a copy of our siteConfigOrg text as a file in the nix store.
  # We need it in the store so it can be consumed at build-time by the emacsWithPackagesFromUsePackage.
  siteConfigOrgFile = pkgs.writeText "site-config.org" config.custom.emacs.siteConfigOrg;
  # This uses the nix-community overlay for emacs. https://github.com/nix-community/emacs-overlay
  #  Elpa and Melpa are available thru this, as are special builds of emacs itself.
  # See here for details: https://nixos.wiki/wiki/Emacs
  # We use the emacsWithPackagesFromUsePackage from the overlay as our "package" to have it automatically generate most of our config, and
  # automatically parse out the emacs packages declared in our config with 'use-package'.
  #
  # We need to reference this a couple times.
  myEmacsPackageWithConfig = (pkgs.emacsWithPackagesFromUsePackage {
        # Emacs package to use as base, this is the latest that's not a nightly.
        package = pkgs.emacs-unstable;

        # Override package set if needed
        override = epkgs: epkgs // {
          # Add any custom package overrides here if needed
        };

        # Additional packages not declared with use-package in the config
        extraEmacsPackages = epkgs: with epkgs; [
          use-package
          org
        ];

        # Always ensure all the packages are installed upfront, don't wait to see
        # if they're ever enabled before installing them. This is un-lazying the install
        # for the packages.
        alwaysEnsure = true;
        # we want to allow non-tangled code blocks for examples as well.
        alwaysTangle = false;
        # This actually controls whether the config= is purely used for getting the list of
        # use-packages that should be installed with nix (=false), or if the file should also
        # be generated into the site-lisp/ folder so it gets loaded (=true).
        # When enabled, the (potentially tangled) config file is set as the site-lisp/default.el
        defaultInitFile = true;
        # org-mode files can only be detected if they are actual files with a '.org' extension.
        # The actual file is read at build-time, so it must be in the nix store.
        config = siteConfigOrgFile;
      });
in {
  options.custom.emacs = {
    siteConfigOrg = lib.mkOption {
      # This must be type 'lines' and not 'str' so we can use lib.mkDefault, lib.mkBefore, and lib.mkAfter to construct it.
      type = lib.types.lines;
      # Set no default here, we set it in the config block with lib.mkDefault instead.
      default = "";
      description = ''
        Text of the site-config.org file to tangle for use as the Emacs config.
        Designed to be used with lib.mkMerge, lib.mkOrder 1000 (default), lib.mkBefore, and lib.mkAfter to construct the full file text.
        The tangled version of this is installed as a site-lisp/default.el file for Emacs to load at startup.
        WARNING: All use-package directives must be included in this file so the packages can be automatically parsed and installed with
                 Nix.
        WARNING: There are restrictions on certain values/variables/settings that are only allowed to be set in the user-specific config.
                 Those must be added to the custom.emacs.configEl option instead.
        '';
      example = ''
        config.custom.emacs.siteConfigOrg = lib.mkMerge [
          (lib.mkBefore ''''
            #+TITLE: My Emacs Config

            * Basic Settings
            #+begin_src emacs-lisp :tangle yes
            ;; Basic settings here
            #+end_src
          '''')
          # default priority order
          (lib.mkOrder 1000 ''''
            * Main Config
            #+begin_src emacs-lisp :tangle yes
            ;; Main config here
            #+end_src
          '''')
          (lib.mkAfter ''''
            * Additional Settings
            #+begin_src emacs-lisp :tangle yes
            ;; Additional settings here
            #+end_src
          '''')
        ];
      '';
    };
    configEl = lib.mkOption {
      # This must be type 'lines' and not 'str' so we can use lib.mkDefault, lib.mkBefore, and lib.mkAfter to construct it.
      type = lib.types.lines;
      # Set no default here, we set it in the config block with lib.mkDefault instead.
      default = "";
      description = ''
        Text of the init.el file to generate in the .emacs.d/ user folder.
        Designed to be used with lib.mkMerge, lib.mkOrder 1000 (default), lib.mkBefore, and lib.mkAfter to construct the full file text.
        WARNING: All use-package directives must be included in the custom.emacs.siteConfigOrg so the packages can be parsed and installed
                 with Nix.
        This setting is primarily just for settings that are only allowed to be set at in the user-specific config.
        '';
      example = ''
        config.custom.emacs.siteConfigOrg = lib.mkMerge [
          # default priority order
          (lib.mkOrder 1000 ''''
            ;; Can only be set in user-specific config
            (setq some-variable some-value)
          '''')
        ];
      '';
    };

  };

  config = {
    home.packages = [
      # Add some nice fonts so we can use them in the emacs config.
      pkgs.nanum-gothic-coding
      pkgs.nerd-fonts.hack
      pkgs.nerd-fonts.iosevka-term
      pkgs.nerd-fonts.jetbrains-mono
      pkgs.nerd-fonts.symbols-only

      # Add all the language-servers from the flake
      inputs.language-servers.packages.${pkgs.stdenv.hostPlatform.system}.default
    ];

    # Just a nice to have so we can see what's in our generated site-config.org file. The file is read-only and in the nix store,
    # and the file was copied, tangled, and set as the site-lisp/default.el at build time, which is how it's actually consumed.
    #
    # This symlink is not actually used by emacs!
    home.file.".emacs.d/site-config.org".source = config.lib.file.mkOutOfStoreSymlink "${siteConfigOrgFile}";

    # Sets up a generated user-specific config file from the custom config value.
    home.file.".emacs.d/init.el".text = config.custom.emacs.configEl;

    # Create a symlink ~/.emacs.d/site-writeable.org that redirects thru a few different symlinks to the real on-disk path of the file in the emacs folder
    # next to this file. The config.lib.file.mkoutOfStoreSymlink will do this for whatever file you pass it.
    # We make this one file writeable, but able to be tracked via Git, for changes that aren't yet ready to be adopted by the read-only nix site-config.org generation.
    home.file.".emacs.d/site-writeable.org".source =  config.lib.file.mkOutOfStoreSymlink "${config.custom.configdir}/programs/emacs/site-writeable.org";

    # Create a symlink ~/.emacs.d/site-writeable.org that redirects thru a few different symlinks to the real on-disk path of the file in the emacs folder
    # next to this file. The config.lib.file.mkoutOfStoreSymlink will do this for whatever file you pass it.
    # We make this one file writeable, but able to be tracked via Git, for changes that aren't yet ready to be adopted by the read-only nix init.el generation.
    # Note: there is also the custom.el file that emacs itself may generate which overrides the read-only nix init.el, but is overridden by this writeable.org file.
    home.file.".emacs.d/writeable.org".source =  config.lib.file.mkOutOfStoreSymlink "${config.custom.configdir}/programs/emacs/writeable.org";

    programs.emacs = {
      enable = true;

      package = myEmacsPackageWithConfig;
    }; # end of programs.emacs

    custom.emacs.configEl = lib.mkMerge [
      (lib.mkAfter ''
        ;; load the custom.el config file Emacs generates whenever you change a setting via the GUI menus
        (let ((custom-config (expand-file-name "custom.el" user-emacs-directory)))
          (setq custom-file  custom-config)
          (when (file-exists-p custom-config)
            (load custom-config)))

        ;; Load and tangle the symlinked writeable.org configuration file if it exists.
        ;; It's not possible to change the tangle output location from here, only the file itself
        ;; can do that, and tangling is only run after the path to the org file is fully resolved.
        (let ((writeable-config (expand-file-name "writeable.org" user-emacs-directory)))
          (when (file-exists-p writeable-config)
            (org-babel-load-file writeable-config)))
      '')
      (lib.mkOrder 1000 ''
        ;; Disable startup screen
        (setq inhibit-startup-message t)
        (setq inhibit-startup-screen t)
      '')
    ];

    # Set the bulk of our custom config here
    custom.emacs.siteConfigOrg = lib.mkMerge [
      (lib.mkAfter ''
        * Load Writeable Site Configuration
        This section loads and tangles the writeable configuration file that can be
        modified outside of the Nix configuration.

        Be careful, the org file we're loading is a symlink to a different location, but we want the tangled output file to be in the folder
        with the symlink, not what the symlink is pointing to.  
        The =org-babel-load-file= automatically resolves the symlink before loading, so make sure the tangle directives in the org file
        list the full destination path explicitly.

        #+begin_src emacs-lisp :tangle yes
        ;; Load and tangle the writeable configuration if it exists.
        (let ((writeable-config (expand-file-name "site-writeable.org" user-emacs-directory)))
          (when (file-exists-p writeable-config)
            (org-babel-load-file writeable-config)))
        #+end_src
      '')
      (lib.mkOrder 1000 '''
        #+TITLE: Emacs Configuration

        *WARNING:* Just the visible file content, this file is not actually loaded from here at run-time!

        * Introduction
        This is a literate Emacs configuration managed by Nix Home Manager.
        All code blocks that should be included in the config must include the tangle directive =:tangle yes=.
        Packages declared with =use-package= are automatically installed by Nix.

        This overally emacs config is separated into 3 levels of configuration files:
        1. =site-config.org=: This file that is a read-only org-mode file defined by the Nix configuration.
        2. =site-writeable.org=: A writeable tangled org-mode file for testing custom changes before they're included in the Nix config.
        3. =custom.el=: A writeable file that the Emacs GUI will automatically created/modify based on changes within the info Menu settings.
        

        The files are loaded in the order listed, which results in later files taking precedence over earlier ones.

        ** Source and Usage

        This file is visible as a symlink from what was generated at nix build time, but is not loaded from where it's visible here in .emacs.d/.

        The file was actually compiled from strings in the nix configuration, written into the nix-store, copied at build-time to a different location
        by the =emacsWithPackagesFromUsePackage= function, tangled by it, and then the tangled result set as the =site-lisp/default.el=.
        To see where the file was really loaded from, use the =M-x describe-variable RET load-history RET= command in emacs, then scroll down to where
        the =site-lisp/default.elc= is loaded to find the path.
        
        This is unfortunately the only way to make use of =emacsWithPackagesFromUsePackage=, which will parse the config to add the necessary nixpkgs for
        the emacs =use-package=s declared in the config rather than the emacs package system run-time installation.
        Emacs tangling only supports generating the tangled output file in the folder next to the org file unless you specify an absolute path for the
        output file as part of the `:tangle` directive(s) in the org file. But doing so breaks the abiliyt of =emacsWithPackagesFromUsePackage= to
        tangle the file at build-time since it requires the tangled el file to be found next to the org file when it does the build-time tangling.
        Since we have to have the file in the nix store for usage at build time, the only way we could populate the file into the .emacs.d/ folder for
        emacs run-time to load would be as a symlink, which results in the tangling always using the folder of the fully-resolved symlink (which is in the
        nix store and not writeable).

        * Core Settings
        ** Basic UI Configuration

        *WARNING*: Cannot disable startup screen from here, it can only be done from the user-specific init.el file.

        #+begin_src emacs-lisp :tangle yes
        ;; Line numbers
        (global-display-line-numbers-mode t)

        ;; Disable line numbers for some modes
        (dolist (mode '(org-mode-hook
                      term-mode-hook
                      shell-mode-hook
                      eshell-mode-hook))
          (add-hook mode (lambda () (display-line-numbers-mode 0))))
        #+end_src

        ** Package System
        #+begin_src emacs-lisp :tangle yes
        ;; Initialize use-package
        (require 'use-package)
        (setq use-package-always-ensure nil)  ;; Nix manages packages
        #+end_src

        * LSP Configuration
        ** LSP Mode
        #+begin_src emacs-lisp :tangle yes
        (use-package lsp-mode
          :init
          (setq lsp-keymap-prefix "C-c l")
          :hook (
                ;; On-demand LSP activation for each language
                (rust-mode . lsp-deferred)
                (python-mode . lsp-deferred)
                (nix-mode . lsp-deferred)
                (go-mode . lsp-deferred)
                (typescript-mode . lsp-deferred)
                (javascript-mode . lsp-deferred)
                (sh-mode . lsp-deferred)
                (c-mode . lsp-deferred)
                (c++-mode . lsp-deferred)
                (java-mode . lsp-deferred)
                (yaml-mode . lsp-deferred)
                (lsp-mode . lsp-enable-which-key-integration))
          :commands (lsp lsp-deferred)
          :config
          (setq lsp-idle-delay 0.5
                lsp-log-io nil
                lsp-completion-provider :capf
                lsp-headerline-breadcrumb-enable nil))
        #+end_src

        ** LSP UI
        #+begin_src emacs-lisp :tangle yes
        (use-package lsp-ui
          :commands lsp-ui-mode
          :config
          (setq lsp-ui-doc-enable t
                lsp-ui-doc-position 'at-point
                lsp-ui-doc-delay 0.5
                lsp-ui-sideline-enable t
                lsp-ui-sideline-show-hover t
                lsp-ui-sideline-show-diagnostics t))
        #+end_src

        * Completion
        ** Company Mode
        #+begin_src emacs-lisp :tangle yes
        (use-package company
          :hook (prog-mode . company-mode)
          :bind (:map company-active-map
                ("<tab>" . company-complete-selection))
          :config
          (setq company-idle-delay 0.2
                company-minimum-prefix-length 1
                company-backends '((company-capf company-dabbrev-code))))
        #+end_src

        * Syntax Checking
        ** Flycheck
        #+begin_src emacs-lisp :tangle yes
        (use-package flycheck
          :hook (prog-mode . flycheck-mode)
          :config
          (setq flycheck-check-syntax-automatically '(save mode-enabled)))
        #+end_src

        * Project Management
        ** Projectile
        #+begin_src emacs-lisp :tangle yes
        (use-package projectile
          :diminish projectile-mode
          :config
          (projectile-mode +1)
          :bind-keymap
          ("C-c p" . projectile-command-map)
          :init
          (setq projectile-project-search-path '("~/projects/")))
        #+end_src

        ** Treemacs
        #+begin_src emacs-lisp :tangle yes
        (use-package treemacs
          :bind
          (:map global-map
                ("C-x t t" . treemacs)))

        (use-package treemacs-projectile
          :after (treemacs projectile))
        #+end_src

        * Version Control
        ** Magit
        #+begin_src emacs-lisp :tangle yes
        (use-package magit
          :bind ("C-x g" . magit-status)
          :config
          (setq magit-display-buffer-function
                #'magit-display-buffer-same-window-except-diff-v1))
        #+end_src

        * UI Enhancements
        ** Theme
        #+begin_src emacs-lisp :tangle yes
        (use-package doom-themes
          :config
          (setq doom-themes-enable-bold t
                doom-themes-enable-italic t)
          (load-theme 'doom-one t)
          (doom-themes-org-config))
        #+end_src

        ** Modeline
        #+begin_src emacs-lisp :tangle yes
        (use-package doom-modeline
          :init (doom-modeline-mode 1)
          :config
          (setq doom-modeline-height 25
                doom-modeline-bar-width 3
                doom-modeline-project-detection 'projectile))

        (use-package all-the-icons
          :if (display-graphic-p))
        #+end_src

        ** Which Key
        #+begin_src emacs-lisp :tangle yes
        (use-package which-key
          :init (which-key-mode)
          :diminish which-key-mode
          :config
          (setq which-key-idle-delay 0.3))
        #+end_src

        * Org Mode Configuration
        ** Basic Settings
        #+begin_src emacs-lisp :tangle yes
        (use-package org
          :config
          (setq org-ellipsis " â–¾"
                org-hide-emphasis-markers t
                org-src-fontify-natively t
                org-src-tab-acts-natively t
                org-edit-src-content-indentation 0
                org-hide-block-startup nil
                org-src-preserve-indentation nil
                org-startup-folded 'content
                org-cycle-separator-lines 2))
        #+end_src

        * Language-Specific Settings
        ** Rust
        #+begin_src emacs-lisp :tangle yes
        (use-package rust-mode
          :mode "\\.rs\\'"
          :config
          (setq rust-format-on-save t))
        #+end_src

        ** Python
        #+begin_src emacs-lisp :tangle yes
        (use-package python-mode
          :mode "\\.py\\'")
        #+end_src

        ** Nix
        #+begin_src emacs-lisp :tangle yes
        (use-package nix-mode
          :mode "\\.nix\\'")
        #+end_src

        ** Go
        #+begin_src emacs-lisp :tangle yes
        (use-package go-mode
          :mode "\\.go\\'")
        #+end_src

        ** TypeScript
        #+begin_src emacs-lisp :tangle yes
        (use-package typescript-mode
          :mode "\\.ts\\'"
          :config
          (setq typescript-indent-level 2))
        #+end_src

        ** YAML
        #+begin_src emacs-lisp :tangle yes
        (use-package yaml-mode
          :mode "\\.ya?ml\\'")
        #+end_src

        ** Markdown
        #+begin_src emacs-lisp :tangle yes
        (use-package markdown-mode
          :mode "\\.md\\'")
        #+end_src

        * Keybindings
        ** Custom Keybindings
        #+begin_src emacs-lisp :tangle yes
        ;; Window navigation
        (global-set-key (kbd "C-c <left>")  'windmove-left)
        (global-set-key (kbd "C-c <right>") 'windmove-right)
        (global-set-key (kbd "C-c <up>")    'windmove-up)
        (global-set-key (kbd "C-c <down>")  'windmove-down)

        ;; Buffer management
        (global-set-key (kbd "C-x k") 'kill-this-buffer)
        #+end_src

        * Performance
        ** Garbage Collection
        #+begin_src emacs-lisp :tangle yes
        ;; Increase GC threshold for better performance
        (setq gc-cons-threshold (* 100 1024 1024))

        ;; Reset GC threshold after startup
        (add-hook 'emacs-startup-hook
                  (lambda ()
                    (setq gc-cons-threshold (* 20 1024 1024))))
        #+end_src
      '')
    ]; # end of custom.emacs.siteConfigOrg
  }; # end of config
}

# vim: ts=2:sw=2:expandtab
